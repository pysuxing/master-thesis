\chapter{Rat形式语法}\label{chap:formal-syntax}

\setlength{\grammarindent}{10em}
\setlength{\grammarparsep}{5pt}
\paragraph{Syntax Rules}
\begin{grammar}
<program>        ::=    <export>+ <top level unit>*

<export>         ::=    'export' <variable>

<top level unit> ::=    <type def>
                 \alt     <variable decl>
                 \alt     <variable def>

<type def>       ::=    'newtype' <type name> '=' <constructor> <type spec>

<constructor>    ::=    <variable>

<type spec>      ::=    <primitive type>
                 \alt     <struct type>
                 \alt     <vector type>
                 \alt     <type spec> '$\to$' <type spec>

<primitive type> ::=    'Int8' | 'UInt8' | 'Int16' | 'UInt16' \alt 'Int32' | 'UInt32' | 'Int64' | 'UInt64'
                 \alt     'Float' | 'Double'

<struct type>    ::=    '\{' <variable decl> (',' <variable decl>)* '\}'

<vector type>    ::=    '[' <type name> ']'

<variable decl>  ::=    <variable> '::' <type name>

<variable def>   ::=    <variable> <variable>* '=' <expression>

<expression>     ::=    <literal>
                 \alt     <variable ref>
                 \alt     <function app>
                 \alt     <lambda exp>
                 \alt     <vector comprehension>
                 \alt     <vector element ref>
                 \alt     <vector slice ref>
                 \alt     <conditional>
                 \alt     <let exp>
                 \alt     <where exp>
                 \alt     '(' <expression> ')'

<literal>        ::=    <number>
                 \alt     <boolean>
                 \alt     <character>
                 \alt     <tuple literal>
                 \alt     <vector literal>

<tuple literal>  ::=    '(' <literal> (',' <literal>)+ ')'

<vector literal> ::=    '[' <start> ',' <end> (',' <step>)? ']'

<start>          ::=    <number>

<end>            ::=    <number>

<step>           ::=    <number>

<variable ref>   ::=    <variable>

<function app>   ::=    <function ref> <expression>*

<function ref>   ::=    <variable>

<lambda exp>     ::=    '$\backslash$' <bind var>+ '$\to$' <expression>

<bind var>       ::=    <variable>

<conditional>    ::=    'if' <test> <if clause> <else clause>?

<test>           ::=    <expression>

<if clause>      ::=    <expression>

<else clause>    ::=    <expression>

<vector comprehension>
                 ::=    '[' <expression> \\('|' <generator> (',' (<generator> | <filter>))+ ']'

<generator>      ::=    <variable> '$\gets$' <variable>

<filter>         ::=    <expression>

<vector element ref>
                 ::=    <variable> '[' <index> ']'

<index>          ::=    <expression>

<vector slice ref>
                 ::=    <variable> '[' <index> ':' <index> (',' <index>)? ']'

<let exp>        ::=    'let' <variable decl>+ <variable def>+ 'in' <expression>

<where exp>      ::=    <expression> 'where' <variable decl>+ <variable def>+
\end{grammar}

\paragraph{Lexical Rules}
\begin{grammar}
<variable>       ::=    <lower id> | <special var>

<lower id>       ::=    <lowercase> (<alpha> | <digit> | '\_')*

<special var>    ::=    '+' | '-' | '*' | '/' | '\^'

<class name>     ::=    <upper id>

<type name>      ::=    <upper id>

<upper id>       ::=    <uppercase> (<alpha> | <digit> | '_')*

<alpha>          ::=    <lowercase> | <uppercase>

<lowercase>      ::=    'a' | 'b' | ... | 'z'

<uppercase>      ::=    'A' | 'B' | ... | 'Z'

<number>         ::=    <integer>
                 \alt     <floating>

<integer>        ::=    <decimal>
                 \alt    ('0O' | '0o') <octal>
                 \alt    ('0X' | '0x') <hexadecimal>

<decimal>        ::=    <digit>+

<octal>          ::=    <octit>+

<hexadecimal>    ::=    <hexit>+

<digit>          ::=    '0' | '1' | '2' | '3' | '5' | '6' | '7' | '8' | '9'

<octit>          ::=    '0' | '1' | '2' | '3' | '5' | '6' | '7'

<hexit>          ::=    <digit> | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'a' | 'b' | 'c' | 'd' | 'e' | 'f'

<floating>       ::=    <decimal> '.' <decimal> <exponent>?
                 \alt     <decimal> <exponent>

<exponent>       ::=    ('e' | 'E') ('+' | '-')? <decimal>

<boolean>        ::=    'true' | 'false'

<character>      ::=    ''' <ascii> '''

<whitespace>     ::=    <space> | <tab> | <newline> | <comment>

<comment>        ::=    '--' <any character>* <newline>
\end{grammar}
